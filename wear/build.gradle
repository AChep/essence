apply plugin: 'com.android.application'

apply plugin: 'kotlin-platform-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion sdk_target
    defaultConfig {
        applicationId "com.artemchep.essence"
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode version_code
        versionName version_name
    }

    signingConfigs {
        release {
            def releaseProps = new Properties()
            if (file("essence-release.properties").exists()) {
                file("essence-release.properties").withInputStream {
                    releaseProps.load(it)
                }
            }

            storeFile = file("essence-release.keystore")
            keyAlias releaseProps['key_alias']
            keyPassword releaseProps['password_key']
            storePassword releaseProps['password_store']
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        signingConfigs.release.storePassword = System.getenv("password_store")
        signingConfigs.release.keyPassword = System.getenv("password_key")
        signingConfigs.release.keyAlias = System.getenv("key_alias")
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
        }
        release {
            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            proguardFile 'proguard-rules.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')

            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation project(":common")
    implementation "com.artemchep.config:config:$config_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    implementation "io.arrow-kt:arrow-core-data:$arrow_version"
    implementation "io.arrow-kt:arrow-core-extensions:$arrow_version"
    implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    implementation "io.arrow-kt:arrow-typeclasses:$arrow_version"
    implementation "io.arrow-kt:arrow-extras-data:$arrow_version"
    implementation "io.arrow-kt:arrow-extras-extensions:$arrow_version"
    implementation "io.arrow-kt:arrow-optics:$arrow_version"
    kapt "io.arrow-kt:arrow-meta:$arrow_version"

    implementation "androidx.core:core-ktx:1.0.1"
    implementation "androidx.recyclerview:recyclerview:$androidx_lib_version"
    implementation "androidx.percentlayout:percentlayout:$androidx_lib_version"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.wear:wear:$androidx_lib_version"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.work:work-runtime-ktx:2.0.0"

    implementation "com.google.android.material:material:1.0.0"

    implementation "com.google.android.support:wearable:$wearable_version"
    implementation "com.google.android.gms:play-services-wearable:16.0.1"
    compileOnly "com.google.android.wearable:wearable:$wearable_version"
}
