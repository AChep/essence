apply plugin: 'com.android.application'

apply plugin: 'kotlin-platform-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

android {
    compileSdkVersion sdk_target

    defaultConfig {
        applicationId "com.artemchep.essence"
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode version_code * 1000000 + 1
        versionName version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    signingConfigs {
        release {
            def releaseProps = new Properties()
            if (file("essence-release.properties").exists()) {
                file("essence-release.properties").withInputStream {
                    releaseProps.load(it)
                }
            }

            storeFile = file("essence-release.keystore")
            keyAlias releaseProps['key_alias']
            keyPassword releaseProps['password_key']
            storePassword releaseProps['password_store']
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        signingConfigs.release.storePassword = System.getenv("password_store")
        signingConfigs.release.keyPassword = System.getenv("password_key")
        signingConfigs.release.keyAlias = System.getenv("key_alias")
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
        }
        release {
            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            proguardFile 'proguard-rules.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')

            signingConfig signingConfigs.release
        }
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

dependencies {
    implementation project(":common")
    implementation "com.artemchep.config:config:$config_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation "com.afollestad.material-dialogs:core:$md_lib_version"

    implementation "io.arrow-kt:arrow-core-data:$arrow_version"
    implementation "io.arrow-kt:arrow-core-extensions:$arrow_version"
    implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    implementation "io.arrow-kt:arrow-typeclasses:$arrow_version"
    implementation "io.arrow-kt:arrow-extras-data:$arrow_version"
    implementation "io.arrow-kt:arrow-extras-extensions:$arrow_version"
    implementation "io.arrow-kt:arrow-optics:$arrow_version"
    kapt "io.arrow-kt:arrow-meta:$arrow_version"

    implementation "androidx.core:core-ktx:$ktx_lib_version"
    implementation "androidx.appcompat:appcompat:$appcompat_lib_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_lib_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    implementation "com.google.android.material:material:1.1.0-alpha10"

    implementation "com.google.android.support:wearable:$wearable_version"
    implementation "com.google.android.gms:play-services-wearable:17.0.0"
    compileOnly "com.google.android.wearable:wearable:$wearable_version"

    implementation "com.google.firebase:firebase-analytics:$firebase_analytics_version"
    implementation "com.crashlytics.sdk.android:crashlytics:$firebase_crashlytics_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'
